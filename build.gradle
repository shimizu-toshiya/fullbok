buildscript {
	repositories {
		mavenCentral()
		maven { url 'http://public-maven.classmethod.info/release' }
	}
	dependencies {
		classpath 'jp.classmethod.aws:gradle-aws-plugin:0.3'
	}
}

ext {
	artifactId = 'fullbok'
	templateBucket = 'cm-public-cfn-templates'
}

version = '1.0-SNAPSHOT'

apply plugin: 'aws'
aws {
	assert project.hasProperty('awsAccessKey') && project.hasProperty('awsSecretKey'): "AWS credential is required"
	accessKeyId awsAccessKey
	secretKey awsSecretKey
	region 'ap-northeast-1'
}

// ======== release tasks ========

task uploadTemplate(type: jp.classmethod.aws.gradle.s3.AmazonS3FileUploadTask) {
	bucketName project.templateBucket
	key "${project.artifactId}/${project.artifactId}-${version}.template"
	file file("${project.artifactId}.template")
	overwrite project.version.endsWith('-SNAPSHOT')
}

task uploadArchives(dependsOn: [uploadTemplate])

// ======== demo tasks ========

apply plugin: 'aws-cloudformation'
cloudFormation {
	stackName project.artifactId
	templateURL "https://${project.templateBucket}.s3.amazonaws.com/${project.artifactId}/${project.artifactId}-${project.version}.template"
	loopTimeout 1800
}

task cfnInit << {
	assert project.hasProperty('keyName'): "keyName parameter is required"
	assert project.hasProperty('availabilityZone'): "availabilityZone parameter is required"
	assert project.hasProperty('sshFrom'): "sshFrom parameter is required"
	assert project.hasProperty('slaveCapacity'): "slaveCapacity parameter is required"
	cloudFormation.stackParams([
		KeyName: project.keyName,
		AvailabilityZone: project.availabilityZone,
		SSHFrom: project.sshFrom,
		SlaveCapacity: project.slaveCapacity
	])
}

awsCfnMigrateStackAndWaitCompleted.mustRunAfter cfnInit
task deployDemo(dependsOn: [cfnInit, awsCfnMigrateStackAndWaitCompleted]) << {
	def stack = cloudFormation.getStack(project.cloudFormation.stackName)
	stack.outputs.each { println "${it.outputKey} = ${it.outputValue}" }
}

task undeployDemo(dependsOn: [cfnInit, awsCfnDeleteStackAndWaitCompleted])
